public class LocationCalloutsContacts {
 
     @future (callout=true)  // future method needed to run callouts from Triggers
      static public void getLocation(id contactId){
        // gather contact info
        Contact a = [SELECT MailingCity,MailingCountry,MailingPostalCode,MailingState,MailingStreet FROM Contact WHERE id =: contactId];
 
        // create an address string
        String address = '';
        if (a.MailingStreet != null)
            address += a.MailingStreet +', ';
        if (a.MailingCity != null)
            address += a.MailingCity +', ';
        if (a.MailingState != null)
            address += a.MailingState +' ';
        if (a.MailingPostalCode != null)
            address += a.MailingPostalCode +', ';
        if (a.MailingCountry != null)
            address += a.MailingCountry;
 
        address = EncodingUtil.urlEncode(address, 'UTF-8');
 
        // build callout
        Http h = new Http();
        HttpRequest req = new HttpRequest();
        req.setEndpoint('http://maps.googleapis.com/maps/api/geocode/json?address='+address+'&sensor=false');
        req.setMethod('GET');
        req.setTimeout(60000);
 
        try{
            // callout
            HttpResponse res = h.send(req);
             System.debug(res.getBody());
            // parse coordinates from response
            JSONParser parser = JSON.createParser(res.getBody());
            double lat = null;
            double lon = null;
            while (parser.nextToken() != null) {
                if ((parser.getCurrentToken() == JSONToken.FIELD_NAME) &&
                    (parser.getText() == 'location')){
                       parser.nextToken(); // object start
                       while (parser.nextToken() != JSONToken.END_OBJECT){
                           String txt = parser.getText();
                           parser.nextToken();
                           if (txt == 'lat')
                               lat = parser.getDoubleValue();
                           else if (txt == 'lng')
                               lon = parser.getDoubleValue();
                       }
 
                }
            }
 
            // update coordinates if we get back
            if (lat != null){
                a.Location__Latitude__s = lat;
                a.Location__Longitude__s = lon;
                update a;
            }
 
        } catch (Exception e) {
        }
    }
}